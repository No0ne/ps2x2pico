;
; Copyright (c) 2025 No0ne (https://github.com/No0ne)
;           (c) 2023 Dustin Hoffman
;
; SPDX-License-Identifier: MIT
;

.program ps2send
restart:
  set    pindirs, 0                 // clock high
  irq    clear 0                    // clear busy flag
  pull   ifempty block              // move new byte into OSR
  irq    nowait 0                   // set busy flag
  set    x, 10                      // number of bits to send out

loop:
  set    pindirs, 0                 // clock high
  jmp    pin, send                  // if clock is high, host is still receiving data
  out    null, 32                   // clock was low, clear OSR
  irq    wait 4 rel                 // host wants to send data, notify of failure to send data
  jmp    restart                    // and wait for restart

send:
  out    pindirs, 1                 // send out data
  set    pindirs, 1                 // set clock low
  nop                               // cycle delay
  jmp    x--, loop                  // iterate


.program ps2receive
restart:
  set    pindirs, 0                 // set clock and data high
  wait   1 pin, 1                   // wait for clock to be pulled high
  wait   0 irq, 0                   // wait for busy flag to get cleared
  jmp    pin, restart               // restart if not receiving
  set    pindirs, 2                 // clock low
  set    x, 8                       // set loop counter

loop:
  set    pindirs, 0                 // clock high
  in     pins, 1                    // receive a bit into ISR
  set    pindirs, 2                 // clock low
  jmp    x--, loop                  // iterate
  set    pindirs, 0                 // clock high
  nop                               // cycle delay
  set    pindirs, 2                 // data high, clock low
  set    pindirs, 3                 // data low, clock low
  nop                               // cycle delay
  set    pindirs, 1                 // data low, clock high
  nop                               // cycle delay

% c-sdk {

  void ps2send_program_init(PIO pio, uint sm, uint offset, u8 data_pin) {
    pio_sm_config c = ps2send_program_get_default_config(offset);

    pio_gpio_init(pio, data_pin);
    pio_gpio_init(pio, data_pin + 1);

    sm_config_set_clkdiv(&c, 1250);
    sm_config_set_jmp_pin(&c, data_pin + 1);
    sm_config_set_set_pins(&c, data_pin + 1, 1);
    sm_config_set_out_pins(&c, data_pin, 1);
    sm_config_set_out_shift(&c, true, false, 11);

    pio_sm_init(pio, sm, offset, &c);
    pio_sm_set_enabled(pio, sm, true);
  }

  void ps2receive_program_init(PIO pio, uint sm, uint offset, u8 data_pin) {
    pio_sm_config c = ps2receive_program_get_default_config(offset);

    sm_config_set_clkdiv(&c, 1875);
    sm_config_set_jmp_pin(&c, data_pin);
    sm_config_set_set_pins(&c, data_pin, 2);
    sm_config_set_in_pins(&c, data_pin);
    sm_config_set_in_shift(&c, true, true, 9);

    pio_sm_init(pio, sm, offset, &c);
    pio_sm_set_enabled(pio, sm, true);
  }

%}
