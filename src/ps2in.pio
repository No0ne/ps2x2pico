;
; Copyright (c) 2024 No0ne (https://github.com/No0ne)
;
; SPDX-License-Identifier: MIT
;

.program ps2in

receivecheck:
  jmp    pin, sendcheck
  set    x, 9

receiveloop:
  wait   0 pin, 1
  in     pins, 1
  wait   1 pin, 1
  jmp    x--, receiveloop

  wait   0 pin, 1
  set    pindirs, 1             [7]
  nop                           [7]
  nop                           [7]
  push   block                  [7]
  nop                           [7]
  nop                           [7]
  set    pindirs, 0
  jmp    receivecheck

sendcheck:
  jmp    !osre, send
  jmp    receivecheck

send:
  set    pindirs, 1             [7]
  out    pindirs, 1             [7]
  set    x, 9                   [7]
  set    pindirs, 0

sendloop:
  wait   0 pin, 1
  out    pindirs, 1
  wait   1 pin, 1
  jmp    x--, sendloop
  wait   0 pin, 1
  wait   1 pin, 1

% c-sdk {
  void ps2in_program_init(PIO pio, uint sm, uint offset, uint dat) {
    pio_sm_config c = ps2in_program_get_default_config(offset);
    
    u8 clk = dat + 1;
    pio_gpio_init(pio, clk);
    pio_gpio_init(pio, dat);
    
    sm_config_set_clkdiv(&c, 960);
    sm_config_set_jmp_pin(&c, clk);
    sm_config_set_set_pins(&c, clk, 1);
    sm_config_set_out_pins(&c, dat, 1);
    sm_config_set_out_shift(&c, true, true, 11);
    sm_config_set_in_pins(&c, dat);
    sm_config_set_in_shift(&c, true, false, 0);
    
    pio_sm_init(pio, sm, offset, &c);
    pio_sm_set_enabled(pio, sm, true);
  }
%}